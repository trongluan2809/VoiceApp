buildscript {
    repositories {
        mavenLocal()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

repositories {
    mavenLocal()
    jcenter()
    google()
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'digital.wup.android-maven-publish' // 'gradle publishToMavenLocal' and 'publish' tasks

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        consumerProguardFiles 'consumer-proguard-rules.pro'
        buildFeatures {
            viewBinding = true
        }
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

// https://docs.gradle.org/current/userguide/publishing_maven.html

task javadoc(type: Javadoc) {
    failOnError = false // options.addStringOption('Xdoclint:none', '-quiet')
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

def prop(String name) {
    project.hasProperty(name) ? project.getProperty(name) : null
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.github.axet'
            artifactId 'android-audio-library'
            version android.defaultConfig.versionName

            from components.android
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Android Audio Library'
                description = 'Android Audio Common classes.'
                url = 'https://gitlab.com/axet/android-audio-library'
                licenses {
                    license {
                        name = 'GNU LESSER GENERAL PUBLIC LICENSE 3.0'
                        url = 'http://www.gnu.org/copyleft/lesser.html'
                    }
                }
                developers {
                    developer {
                        id = 'axet'
                        name = 'Alexey Kuznetsov'
                        email = 'axet@me.com'
                    }
                }
                scm {
                    connection = 'scm:git:' + pom.url.get()
                    developerConnection = 'scm:git:' + pom.url.get()
                    url = pom.url
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username prop('ossrhUsername')
                password prop('ossrhPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    //api 'com.github.axet:android-library:1.33.6' // api project(':android-library')
    api 'com.google.android.material:material:1.4.0'
    api 'org.apache.commons:commons-math3:3.6.1'
    api 'com.github.axet:vorbis:1.0.5' // compile project(':android-vorbis')
    api 'com.github.axet:lame:1.0.6' // compile project(':android-lame')
    api 'com.github.axet:opus:1.0.5' // compile project(':android-opus')
    api 'com.github.axet:jebml:0.0.2' // compile project(':jebml')
    api 'com.github.axet:java-flac-encoder:0.3.8'
    api 'org.gagravarr:vorbis-java-core:0.8'
    api 'com.github.axet:jssrc:1.0.2-2'
    api 'uk.me.berndporr:iirj:1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.gms:play-services-measurement-api:19.0.0'
    implementation project(path: ':android-library')
    //implementation project(path: ':android-library')
    testImplementation 'junit:junit:4.13.2'

    // RxAndroid
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.13'
    // RxBinding
    implementation 'com.jakewharton.rxbinding4:rxbinding:4.0.0'
    // lib ads
    implementation 'com.github.quangchienictu:AmazicAdsLibrary:1.1.6'
}
