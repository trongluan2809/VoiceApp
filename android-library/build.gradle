buildscript {
    repositories {
        mavenLocal()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

repositories {
    mavenLocal()
    jcenter()
    google()
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'digital.wup.android-maven-publish' // 'gradle publishToMavenLocal' and 'publish' tasks

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 30// 24+ - unable to start intent with 'file://'
        versionCode  634
        versionName  "1.34.1"
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

// https://docs.gradle.org/current/userguide/publishing_maven.html

task javadoc(type: Javadoc) {
    failOnError = false // options.addStringOption('Xdoclint:none', '-quiet')
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.github.axet'
            artifactId 'android-library'
            version android.defaultConfig.versionName

            from components.android
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Android Library'
                description = 'Android Simple Widgets and Support classes.'
                url = 'https://gitlab.com/axet/android-library'
                licenses {
                    license {
                        name = 'GNU LESSER GENERAL PUBLIC LICENSE 3.0'
                        url = 'http://www.gnu.org/copyleft/lesser.html'
                    }
                }
                developers {
                    developer {
                        id = 'axet'
                        name = 'Alexey Kuznetsov'
                        email = 'axet@me.com'
                    }
                }
                scm {
                    connection = 'scm:git:' + pom.url.get()
                    developerConnection = 'scm:git:' + pom.url.get()
                    url = pom.url
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.findProperty('ossrhUsername')
                password project.findProperty('ossrhPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    testImplementation 'junit:junit:4.12'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.appcompat:appcompat:1.0.0'
    api 'androidx.preference:preference:1.0.0' // 25.4.0+ - min sdk 14 for SwitchCompat
    api 'androidx.mediarouter:mediarouter:1.0.0'
    api 'cz.msebera.android:httpclient:4.4.1.2'
    api 'commons-codec:commons-codec:1.10'
    api 'org.jsoup:jsoup:1.10.2'
    api 'commons-io:commons-io:2.5'
    api('net.i2p.android.ext:floatingactionbutton:1.10.1') { exclude group: 'com.android.support' }
    api('com.github.axet:java-unrar:1.7.0-4') { exclude group: 'commons-logging', module: 'commons-logging' } // java 1.8
    api 'com.github.axet:zip4j:2.7.0'
}
